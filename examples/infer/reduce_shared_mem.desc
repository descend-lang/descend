fn reduce_shared_mem<gs: nat, bs: nat, a: prv, b: prv>(
    ha_array: &a shrd cpu.mem [i32; gs*bs],
    h_output: &b uniq cpu.mem [i32; gs]
) -[t: cpu.thread]-> () {
    let mut gpu = gpu_device(0);

    let a_array = gpu_alloc_copy(&uniq gpu, ha_array);
    let mut out_array = gpu_alloc_copy(&uniq gpu, &shrd *h_output);
    exec::<gs, bs>(
        &uniq gpu,
        (&shrd a_array, &uniq out_array),
        | inputs: (&shrd gpu.global [i32; gs*bs],
          &uniq gpu.global [i32; gs])| -[grid: gpu.grid<X<64>, X<1024>>]-> () {
            let block_group = group::<1024>(to_view(inputs.0));
            let view_out = group_mut::<1>(to_view_mut(inputs.1));
            // TODO remove decl and move out tmp declaration
            decl {
                let mut tmp: [i32; bs] @ gpu.shared
            } parfor block in grid with ib, out_elem from block_group, view_out {
                 let tmp_view = to_view_mut(&uniq tmp);
                 parfor _ in block
                 with inp, tmp_in from ib, tmp_view { *tmp_in = *inp };

                 for_nat k in halved_range(bs/2) {
                    let tmp_view = to_view_mut(&uniq tmp);
                    let split_tmp_view = (split uniq 2*k (*tmp_view)).0;
                    let active_halves = split uniq k (*split_tmp_view);

                    let active_half0 = active_halves.0;
                    let active_half1 = active_halves.1;

                    // Fixme: At this point 's1 -> {tmp, split_tmp_view, tmp_view, tmp}
                    par_branch split_exec(X) k block {
                        active =>
                            parfor _ in active
                            with a, b from active_half0, active_half1 {
                                * a = * a + * b
                            },
                        inactive => { () }
                    }
                 };
                 let tmp_view2 = to_view_mut(&uniq tmp);
                 let tmp_res = (split uniq 1 (*tmp_view2)).0;
                 par_branch split_exec(X) 1 block {
                    single_thread =>
                        parfor _ in single_thread
                        with out, tmp_val from out_elem, tmp_res {
                            *out = *tmp_val
                        },
                    rest => { () }
                 }
            }
        }
    );
    // Todo
    //  There is no check for n == 64 (which is NOT the case). Therefore it is wrong to specify
    //  [i32; n] as the array type. However, this still type checks.
    copy_to_host(&shrd out_array, &uniq *h_output);
    let mut res = 0;
    for x in h_output {
        res = res + *x
    }
}